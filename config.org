#+TODO: ASSESS(a) TRIAL(t!) | ADOPT(d!) DEPRECATED(k@)
#+title: Emacs configuration

* About me
#+begin_src emacs-lisp
(setq user-full-name "Yejun Su"
      user-mail-address "goofan.su@gmail.com")
#+end_src

* Appearance
** Theme
#+begin_src emacs-lisp
(if (display-graphic-p)
    (setq doom-theme 'modus-operandi)
  (setq doom-theme 'modus-vivendi))
#+end_src

#+begin_src emacs-lisp
(use-package! modus-themes
  :config
  (setq modus-themes-completions
        '((matches . (extrabold underline))
          (selection . (semibold italic))))
  (setq modus-themes-headings
        '((1 . (variable-pitch 1.2))
          (2 . (variable-pitch 1.1))
          (3 . (variable-pitch 1.05))
          (4 . (1.0))
          (agenda-date . (1.2))
          (agenda-structure . (variable-pitch light 1.6))
          (t . (1.1)))))
#+end_src

** Font
#+begin_src emacs-lisp
(setq doom-font (font-spec :family "JetBrains Mono" :size 16)
      doom-variable-pitch-font (font-spec :family "Iosevka" :size 16))
#+end_src

** Icon
#+begin_src emacs-lisp
(setq nerd-icons-font-names '("SymbolsNerdFontMono-Regular.ttf"))
#+end_src

* Better defaults
Ref: https://tecosaur.github.io/emacs-config/config.html#better-defaults

** Simple Settings
#+begin_src emacs-lisp
(setq-default
 delete-by-moving-to-trash t                      ; Delete files to trash
 window-combination-resize t                      ; take new window space from all other windows (not just current)
 x-stretch-cursor t)                              ; Stretch cursor to the glyph width

(setq undo-limit 80000000                         ; Raise undo-limit to 80Mb
      evil-want-fine-undo t                       ; By default while in insert all changes are one big blob. Be more granular
      auto-save-default t                         ; Nobody likes to loose work, I certainly don't
      truncate-string-ellipsis "â€¦"                ; Unicode ellispis are nicer than "...", and also save /precious/ space
      password-cache-expiry nil                   ; I can trust my computers ... can't I?
      scroll-margin 2)                            ; It's nice to maintain a little margin

(display-time-mode 1)                             ; Enable time in the mode-line
(global-subword-mode 1)                           ; Iterate through CamelCase words
#+end_src

** Frame sizing
#+begin_src emacs-lisp
(add-to-list 'default-frame-alist '(height . 24))
(add-to-list 'default-frame-alist '(width . 80))
#+end_src

** Windows
#+begin_src emacs-lisp
(setq evil-vsplit-window-right t
      evil-split-window-below t)

(defadvice! prompt-for-buffer (&rest _)
  :after '(evil-window-split evil-window-vsplit)
  (consult-buffer))
#+end_src

** Evil
#+begin_src emacs-lisp
(after! evil
  (setq evil-ex-substitute-global t     ; I like my s/../.. to by global by default
        evil-move-cursor-back nil       ; Don't move the block cursor when toggling insert mode
        evil-kill-on-visual-paste nil)) ; Don't put overwritten text in the kill ring
#+end_src

** Projectile
#+begin_src emacs-lisp
(setq projectile-ignored-projects
      (list "~/" "/tmp" (expand-file-name "straight/repos" doom-local-dir)))
(defun projectile-ignored-project-function (filepath)
  "Return t if FILEPATH is within any of `projectile-ignored-projects'"
  (or (mapcar (lambda (p) (s-starts-with-p p filepath)) projectile-ignored-projects)))
#+end_src

** DEPRECATED Smart parentheses
CLOSED: [2023-10-18 Wed 14:06]
:LOGBOOK:
- State "DEPRECATED" from              [2023-10-18 Wed 14:06] \\
  not used
:END:
#+begin_src emacs-lisp :tangle no
(sp-local-pair
 '(org-mode)
 "<<" ">>"
 :actions '(insert))
#+end_src

* Organize my life
** Org Mode
Ref: https://blog.aaronbieber.com/2016/09/24/an-agenda-for-life-with-org-mode.html

#+begin_src emacs-lisp
(setq org-directory "~/org/")
#+end_src

#+begin_src emacs-lisp
(after! org
  (setq org-log-into-drawer t)
  (setq org-log-done 'time)
  (setq org-todo-keywords
        '((sequence "TODO(t)" "HOLD(h@/!)" "|" "DONE(d!)" "KILL(k@)")))
  (setq org-todo-keyword-faces
        '(("KILL" . (:inherit (italic +org-todo-cancel)))
          ("DEPRECATED" . (:inherit (italic +org-todo-cancel)))))

  (setq org-capture-templates
        '(("t" "Tasks" entry (file +org-capture-todo-file) "* TODO %?\n%i" :prepend t)
          ("j" "Journal" entry (file+olp+datetree +org-capture-journal-file) "* %U %?\n%i")))

  ;; Export
  (setq org-export-with-sub-superscripts nil)

  ;; Mark the content of org-babel source block
  (define-key org-mode-map (kbd "C-M-S-h") #'org-babel-mark-block)

  ;; Custom agenda
  (defun air-org-skip-subtree-if-priority (priority)
    "Skip an agenda subtree if it has a priority of PRIORITY.
PRIORITY may be one of the characters ?A, ?B, or ?C."
    (let ((subtree-end (save-excursion (org-end-of-subtree t)))
          (pri-value (* 1000 (- org-lowest-priority priority)))
          (pri-current (org-get-priority (thing-at-point 'line t))))
      (if (= pri-value pri-current)
          subtree-end
        nil)))
  (defun air-org-skip-subtree-if-habit ()
    "Skip an agenda entry if it has a STYLE property equal to \"habit\"."
    (let ((subtree-end (save-excursion (org-end-of-subtree t))))
      (if (string= (org-entry-get nil "STYLE") "habit")
          subtree-end
        nil)))
  (setq org-agenda-custom-commands
        '(("d" "My daily agenda"
           ((tags "PRIORITY=\"A\""
                  ((org-agenda-skip-function '(org-agenda-skip-entry-if 'todo 'done))
                   (org-agenda-overriding-header "High-priority unfinished tasks:")))
            (agenda "" ((org-agenda-span 1)
                        (org-agenda-start-day "+0d")))
            (alltodo ""
                     ((org-agenda-overriding-header "ALL normal priority tasks:")
                      (org-agenda-skip-function '(or (air-org-skip-subtree-if-habit)
                                                     (air-org-skip-subtree-if-priority ?A)
                                                     (org-agenda-skip-if nil '(scheduled deadline))))))))))

  ;; Save all org buffers after certain actions are performed: setting todo item
  ;; state, scheduling an item, assigning a deadline, and logging a note.
  (defmacro func-ignore (fnc)
    "Return function that ignores its arguments and invokes FNC."
    `(lambda (&rest _rest)
       (funcall ,fnc)))
  (advice-add 'org-deadline       :after (func-ignore #'org-save-all-org-buffers))
  (advice-add 'org-schedule       :after (func-ignore #'org-save-all-org-buffers))
  (advice-add 'org-store-log-note :after (func-ignore #'org-save-all-org-buffers))
  (advice-add 'org-todo           :after (func-ignore #'org-save-all-org-buffers)))
#+end_src

** Taking smart notes
*** DEPRECATED Org-roam
CLOSED: [2023-10-08 Sun 21:16]
:LOGBOOK:
- State "DEPRECATED" from              [2023-10-08 Sun 21:16] \\
  Replaced with denote.
:END:
#+begin_src emacs-lisp :tangle no
(setq org-roam-directory "~/org-roam/")

(after! org-roam
  (setq org-roam-capture-templates
        '(("m" "main" plain "%?"
           :if-new (file+head "main/%<%Y%m%d%H%M%S>-${slug}.org" "#+title: ${title}\n")
           :immediate-finish t
           :unnarrowed t)
          ("r" "reference" plain "%?"
           :if-new (file+head "reference/%<%Y%m%d%H%M%S>-${slug}.org" "#+title: ${title}\n")
           :immediate-finish t
           :unnarrowed t)
          ("p" "project" plain "%?"
           :if-new (file+head "project/%<%Y%m%d%H%M%S>-${slug}.org" "#+title: ${title}\n")
           :immediate-finish t
           :unnarrowed t)
          ("a" "articles" plain "%?"
           :if-new (file+head "articles/${slug}.org" "#+title: ${title}\n#+filetags: :article:\n")
           :immediate-finish t
           :unnarrowed t)))

  (cl-defmethod org-roam-node-type ((node org-roam-node))
    "Return the TYPE of NODE."
    (condition-case nil
        (file-name-nondirectory
         (directory-file-name
          (file-name-directory
           (file-relative-name (org-roam-node-file node) org-roam-directory))))
      (error "")))
  (setq org-roam-node-display-template
        (concat "${type:15} ${title:*} " (propertize "${tags:60}" 'face 'org-tag)))

  (defun tag-new-org-roam-node-as-draft ()
    (org-roam-tag-add '("draft")))
  (add-hook 'org-roam-capture-new-node-hook #'tag-new-org-roam-node-as-draft))

(map! :leader
      "nF" #'org-roam-node-find)
#+end_src

*** TRIAL Denote
:LOGBOOK:
- State "TRIAL"      from              [2023-10-08 Sun 19:04]
:END:
#+begin_src emacs-lisp
(use-package! denote
  :custom
  (denote-directory "~/src/notes")
  (denote-known-keywords '("emacs" "nix" "ruby" "elixir" "sourcehut" "web" "security")))

(with-eval-after-load 'org-capture
  (add-to-list 'org-capture-templates
               '("n" "Notes" plain
                 (file denote-last-path)
                 #'denote-org-capture
                 :no-save t
                 :immediate-finish nil
                 :kill-buffer t
                 :jump-to-captured t)))

(defun yejun/browse-notes ()
  (interactive)
  (doom-project-find-file denote-directory))

(defun yejun/search-notes (query)
  "Perform a text search on `org-directory'."
  (interactive
   (list (if (doom-region-active-p)
             (buffer-substring-no-properties
              (doom-region-beginning)
              (doom-region-end))
           "")))
  (+default/search-project-for-symbol-at-point
   query denote-directory))

(map! :leader
      :desc "Create note"                 "nn" #'denote
      :desc "Create note in type"         "nN" #'denote-type
      :desc "Create note in date"         "nd" #'denote-date
      :desc "Create note in subdirectory" "nD" #'denote-subdirectory
      :desc "Create note in template"     "nT" #'denote-template
      :desc "Link note"                   "ni" #'denote-link
      :desc "Browse notes"                "nF" #'yejun/browse-notes
      :desc "Search notes"                "ns" #'yejun/search-notes)
#+end_src

*** Citation management
#+begin_src emacs-lisp
(setq yejun/reference-file (expand-file-name "reference.bib" denote-directory))
(setq yejun/reference-directory (expand-file-name "reference" denote-directory))
#+end_src

#+begin_src emacs-lisp
(use-package! citar
  :custom
  (citar-bibliography (list yejun/reference-file))
  (citar-notes-paths (list yejun/reference-directory)))

(map! :leader
      :desc "Bibliographic files" "nB" #'citar-open)
#+end_src

#+begin_src emacs-lisp
(after! oc
  (setq org-cite-global-bibliography (list yejun/reference-file)))
#+end_src

** Make presentations
https://gitlab.com/oer/org-re-reveal

#+begin_src emacs-lisp
(after! org-re-reveal
  (setq org-re-reveal-transition "none"))
#+end_src

** Import documents to Org Mode
#+begin_src emacs-lisp
(use-package! org-pandoc-import
  :after org)
#+end_src

** Publish websites
#+begin_src emacs-lisp
(use-package! ox-hugo
  :custom
  (org-hugo-delete-trailing-ws nil))
#+end_src

* Editing
** Don't check syntax when editing
#+begin_src emacs-lisp
(after! flycheck
  (delq 'idle-change flycheck-check-syntax-automatically))
#+end_src

* Coding
** Elixir
#+begin_src emacs-lisp
(use-package! elixir-mode
  :hook (before-save . elixir-format-before-save)
  :config
  (defun elixir-format-before-save ()
    (when (derived-mode-p 'elixir-mode)
      (eglot-format-buffer))))
#+end_src

#+begin_src emacs-lisp
(set-eglot-client! 'elixir-mode '("nextls" "--stdio"))
#+end_src

** Nix
#+begin_src emacs-lisp
(use-package! nix-mode
  :custom (nix-nixfmt-bin "nixfmt")
  :hook
  (before-save . nix-format-before-save)
  (nix-mode . yejun/display-nix-nixfmt-bin))

(defun yejun/display-nix-nixfmt-bin ()
  (add-to-list 'mode-line-process '(:eval nix-nixfmt-bin)))

(defun yejun/toggle-nixfmt-bin ()
  (interactive)
  (if (string-match "nixfmt" nix-nixfmt-bin)
      (setq nix-nixfmt-bin "nixpkgs-fmt")
    (setq nix-nixfmt-bin "nixfmt"))
  (message (concat "nix-nixfmt-bin is " nix-nixfmt-bin)))

(map! :leader
      :desc "nix-nixfmt-bin" "tn" #'yejun/toggle-nixfmt-bin)
#+end_src

*** Execute nix code block with org-babel
#+begin_src emacs-lisp
(after! org
  (defun org-babel-execute:nix (body params)
    (with-temp-buffer
      (insert body)
      (shell-command-on-region
       (point-min) (point-max)
       "nix-instantiate --eval - <<EOF\n$(cat)\nEOF"
       (current-buffer)
       t)
      (buffer-string)))

  (add-to-list 'org-babel-load-languages '(nix . t))
  (org-babel-do-load-languages 'org-babel-load-languages org-babel-load-languages))
#+end_src

** Web
#+begin_src emacs-lisp
(use-package! web-mode
  :custom
  (web-mode-markup-indent-offset 2)
  (web-mode-css-indent-offset 2)
  (web-mode-code-indent-offset 2))
#+end_src

#+begin_src emacs-lisp
(use-package! css-mode
  :custom
  (css-indent-offset 2))
#+end_src

* Tools
** Emacs server
#+begin_src emacs-lisp
(when (display-graphic-p)
  (setenv "EMACS_SERVER_NAME" "gui"))
#+end_src

** Forge
#+begin_src emacs-lisp
(use-package! forge
  :config
  (setq forge-topic-list-limit '(20 . 5)))
#+end_src

** Github
*** CLI
https://cli.github.com/manual/

#+begin_src emacs-lisp
(defun yejun/gh-pr-create ()
  (interactive)
  (shell-command "gh pr create -w"))

(defun yejun/gh-pr-view ()
  (interactive)
  (shell-command "gh pr view -w"))
#+end_src

*** Gist
https://gist.github.com/

#+begin_src emacs-lisp
(defun yejun/gist-region-or-buffer (&optional p)
  (interactive "P")
  (let ((filename (buffer-name))
        (output-buffer " *gist-output*")
        (public (if p " --public" "")))
    (shell-command-on-region
     (if (use-region-p) (region-beginning) (point-min))
     (if (use-region-p) (region-end) (point-max))
     (concat "gh gist create --filename " filename public " -")
     output-buffer)
    (with-current-buffer output-buffer
      (goto-char (point-max))
      (forward-line -1)
      (kill-new (thing-at-point 'line)))
    (kill-buffer output-buffer)))

(map! :leader
      :desc "Gist buffer/region"  "cg" #'yejun/gist-region-or-buffer)
#+end_src

** SourceHut
*** Paste
https://paste.sr.ht

#+begin_src emacs-lisp
(defun yejun/paste-region-or-buffer (&optional p)
  (interactive "P")
  (let ((filename (read-string "Enter filename: " (buffer-name)))
        (output-buffer " *paste-output*")
        (public (if p " --visibility public" "")))
    (shell-command-on-region
     (if (use-region-p) (region-beginning) (point-min))
     (if (use-region-p) (region-end) (point-max))
     (concat "hut paste create --name \"" filename "\"" public)
     output-buffer)
    (with-current-buffer output-buffer
      (goto-char (point-max))
      (forward-line -1)
      (kill-new (thing-at-point 'line)))
    (kill-buffer output-buffer)))

(map! :leader
      :desc "Paste buffer/region" "cp" #'yejun/paste-region-or-buffer)
#+end_src

** ChatGPT
#+begin_src emacs-lisp
(use-package! chatgpt-shell
  :custom
  (chatgpt-shell-model-version 2)
  (chatgpt-shell-welcome-function nil)
  (chatgpt-shell-openai-key (lambda () (+pass-get-secret "api.openai.com")))
  :config
  (set-popup-rules!
    '(("^\\*chatgpt\\*" :side bottom :size 0.5 :select t)
      ("^ChatGPT>" :side bottom :size 0.5 :select t))))

(map! :leader
      :prefix ("z" . "chatgpt-shell")
      "z" #'chatgpt-shell
      "b" #'chatgpt-shell-prompt
      "c" #'chatgpt-shell-prompt-compose
      "s" #'chatgpt-shell-send-region
      "S" #'chatgpt-shell-send-and-review-region
      "e" #'chatgpt-shell-explain-code
      "r" #'chatgpt-shell-refactor-code)
#+end_src

** Dash.app
#+begin_src emacs-lisp
(map! :leader
      "sk" #'dash-at-point
      "sK" #'dash-at-point-with-docset)
#+end_src

** TRIAL IRC client
:LOGBOOK:
- State "TRIAL"      from "DEPRECATED" [2023-09-21 Thu 01:12]
- State "DEPRECATED" from              [2023-09-07 Thu 11:27] \\
  Use https://chat.sr.ht/ instead.
:END:

*** Use IRC bouncer provided by chat.sr.ht
Manual: https://man.sr.ht/chat.sr.ht/quickstart.md#connecting-without-a-client-plugin

#+begin_src emacs-lisp
(set-irc-server! "sourcehut/liberachat"
  '(:host "chat.sr.ht"
    :port 6697
    :use-tls t
    :nick "goofans"
    :realname "Yejun Su"
    :sasl-username "goofansu/irc.libera.chat"
    :sasl-password (lambda (&rest _) (+pass-get-secret "chat.sr.ht"))
    :nickserv-password (lambda (&rest _) (+pass-get-secret "irc.libera.chat"))))

(global-set-key (kbd "s-k") #'+irc/jump-to-channel)
#+end_src

*** Hide names list when joining channels:
#+begin_src emacs-lisp
(after! circe
  (circe-set-display-handler "353" 'circe-display-ignore)
  (circe-set-display-handler "366" 'circe-display-ignore))
#+end_src

*** Pull latest chat history:
#+begin_src emacs-lisp
(defun yejun/pull-chat-history ()
  (interactive)
  (circe-command-QUOTE
   (format "CHATHISTORY LATEST %s * 100" circe-chat-target)))

(map! :map circe-channel-mode-map
      :localleader "P" #'yejun/pull-chat-history)
#+end_src

** Password management
I use [[https://www.passwordstore.org/][pass]] and its extension [[https://github.com/tadfisher/pass-otp][pass-otp]] to store TOTP and 2FA recovery codes.

#+begin_src emacs-lisp
(defun yejun/otp-key-uri (issuer secret)
  "Create and copy the OTP key URI consisting of issuer and secret."
  (interactive (list (read-string "Issuer: ")
                     (read-passwd "Secret: " t)))
  (let* ((secret (replace-regexp-in-string "\\s-" "" secret))
         (otp-uri (format "otpauth://totp/totp-secret?secret=%s&issuer=%s" secret issuer)))
    (kill-new otp-uri)
    (message "OTP key URI created and copied.")))

(map! :leader
      (:prefix-map ("o p" . "pass")
       :desc "Copy password"         "p" #'password-store-copy
       :desc "Copy OTP token"        "P" #'password-store-otp-token-copy
       :desc "Insert password"       "i" #'password-store-insert
       :desc "Insert OTP"            "I" #'password-store-otp-insert
       :desc "Edit password"         "e" #'password-store-edit
       :desc "Rename password entry" "r" #'password-store-rename
       :desc "Remove password entry" "R" #'password-store-remove
       :desc "Append OTP"            "a" #'password-store-otp-append
       :desc "Append OTP from image" "A" #'password-store-otp-append-from-image
       :desc "Create OTP key URI"    "o" #'yejun/otp-key-uri))
#+end_src

** GnuPG
#+begin_src emacs-lisp
(defun yejun/decrypt-pgp-file ()
  (interactive)
  (let* ((current-file (buffer-file-name))
         (plain-file (concat current-file ".txt")))
    (epa-decrypt-file current-file plain-file)
    (find-file plain-file)))

(map! :leader
      :desc "Decrypt PGP file" "fm" #'yejun/decrypt-pgp-file)
#+end_src

** DEPRECATED Open Source Map
CLOSED: [2023-09-21 Thu 22:14]
:LOGBOOK:
- State "DEPRECATED" from "TRIAL"      [2023-09-21 Thu 22:14] \\
  Not use often at the moment.
:END:
#+begin_src emacs-lisp :tangle no
(use-package! osm
  :config
  (require 'osm-ol)
  :custom
  (osm-server 'default)
  (osm-copyright t)
  :bind ("C-c m" . osm-prefix-map)
  :hook (osm-mode . evil-emacs-state))
#+end_src

** Magit
*** Add an option to send skip-ci in magit-push
#+begin_src emacs-lisp
(after! magit
  ;; git push with skip-ci option
  (transient-append-suffix 'magit-push "-n"
    '("-s" "Skip CI" "--push-option=skip-ci"))

  ;; git push to all remotes
  (transient-append-suffix 'magit-push "e"
    '("E" "everywhere" magit-push-all))
  (defun magit-push-all (&optional args)
    (interactive (list (magit-push-arguments)))
    (dolist (remote (magit-list-remotes))
      (magit-push-to-remote remote args))))
#+end_src

** Mail
*** DEPRECATED SMTP
CLOSED: [2023-09-15 Fri 12:55]
:LOGBOOK:
- State "DEPRECATED" from              [2023-09-15 Fri 12:55] \\
  Replaced with msmtp configured in the m4ue section.
:END:
Ref: https://www.gnu.org/software/emacs/manual/html_mono/smtpmail.html

#+begin_src emacs-lisp :tangle no
(setq send-mail-function 'smtpmail-send-it
      smtpmail-smtp-server "smtp.gmail.com"
      smtpmail-smtp-service 587
      smtpmail-debug-info t)
#+end_src

*** mu4e
Ref: https://systemcrafters.net/emacs-mail/streamline-your-email-with-mu4e/

#+begin_src emacs-lisp
(after! mu4e
  (setq mu4e-update-interval (* 15 60))

  (set-email-account! "Gmail"
                      '((mu4e-sent-folder       . "/[Gmail]/Sent Mail")
                        (mu4e-drafts-folder     . "/[Gmail]/Drafts")
                        (mu4e-trash-folder      . "/[Gmail]/Trash")
                        (mu4e-refile-folder     . "/[Gmail]/All Mail"))
                      t)

  (setq sendmail-program (executable-find "msmtp")
        send-mail-function #'smtpmail-send-it
        message-sendmail-f-is-evil t
        message-sendmail-extra-arguments '("--read-envelope-from")
        message-send-mail-function #'message-send-mail-with-sendmail)

  (setq mu4e-maildir-shortcuts
        '((:maildir "/Inbox"             :key ?i)
          (:maildir "/[Gmail]/Sent Mail" :key ?s)
          (:maildir "/[Gmail]/Drafts"    :key ?d)
          (:maildir "/[Gmail]/Trash"     :key ?t)
          (:maildir "/[Gmail]/All Mail"  :key ?a)))

  (setq mu4e-bookmarks
        '((:name "Unread messages" :query "flag:unread AND NOT flag:trashed" :key ?u)
          (:name "Today's messages" :query "date:today..now" :key ?t)
          (:name "Last 7 days" :query "date:7d..now" :hide-unread t :key ?w)
          (:name "Messages with images" :query "mime:image/*" :key ?p)
          (:name "SourceHut lists" :query "lists.sr.ht" :key ?s)
          (:name "GitHub notifications" :query "github.com" :key ?g))))
#+end_src

** Mastodon
https://codeberg.org/martianh/mastodon.el

#+begin_src emacs-lisp
(use-package! mastodon
  :custom
  (mastodon-instance-url "https://hachyderm.io")
  (mastodon-active-user "goofansu"))
#+end_src

* Projects
** DEPRECATED .emacs.d
CLOSED: [2023-10-07 Sat 17:11]
:LOGBOOK:
- State "DEPRECATED" from              [2023-10-07 Sat 17:11] \\
  Start this Emacs in terminal.
:END:
I'm building my own Emacs configuration [[https://github.com/goofansu/.emacs.d][here]].

#+begin_src emacs-lisp
(defun yejun/vanilla-emacs--launch ()
  (interactive)
  (let ((default-directory "~/src/.emacs.d/"))
    (start-process "Emacs" nil "emacs" "-q" "-l" "init.el" "config.org")))

(global-set-key (kbd "C-c e") #'yejun/vanilla-emacs--launch)
#+end_src

** nix-config
#+begin_src emacs-lisp
(defun yejun/browse-nix-config ()
  (interactive)
  (doom-project-find-file "~/.config/nix-config"))

(map! :leader
      :desc "Browse nix-config" "fn" #'yejun/browse-nix-config)
#+end_src

** blog
#+begin_src emacs-lisp
(defun yejun/browse-blog ()
  (interactive)
  (doom-project-find-file "~/src/yejun.dev"))

(map! :leader
      :desc "Browse blog" "nA" #'yejun/browse-blog)
#+end_src

* Advices
** Run Nix org-babel source block in Kitty's nix-repl tab
#+begin_src emacs-lisp
(defun kitty--ensure-nix-repl-tab ()
  (unless (zerop (shell-command "kitty @ ls | grep -q '\"title\": \"nix-repl\"'"))
    (shell-command "kitty @ launch --type tab --tab-title nix-repl nix repl")))

(defun kitty--send-region-to-nix-repl-tab ()
  (shell-command-on-region
   (if (use-region-p) (region-beginning) (point-min))
   (if (use-region-p) (region-end) (point-max))
   "kitty @ send-text --match-tab title:nix-repl --stdin"))

(defun org-babel-src-block-language-p (language)
  (let ((block-info (org-element-at-point)))
    (and (eq (car block-info) 'src-block)
         (string= language (org-element-property :language block-info)))))

(defadvice +eval/send-region-to-repl (around my-send-region-to-repl activate)
  (if (and (eq major-mode 'org-mode)
           (org-babel-src-block-language-p "nix"))
      (progn
        (kitty--ensure-nix-repl-tab)
        (kitty--send-region-to-nix-repl-tab))
    ad-do-it))
#+end_src

* Customize
** Use Kagi for +lookup/online
#+begin_src emacs-lisp
(add-to-list '+lookup-provider-url-alist '("Kagi" "https://kagi.com/search?q=%s"))
#+end_src
